plugins {
	id 'org.springframework.boot' version '3.3.1' apply false
	id 'io.spring.dependency-management' version '1.1.5'
	id 'org.jetbrains.kotlin.jvm' version '1.9.24'
	id 'org.jetbrains.kotlin.plugin.spring' version '1.9.24'
	id 'com.diffplug.spotless' version '6.25.0'
}

group = 'com.example.demo'
version = '0.0.1-SNAPSHOT'

allprojects {
	apply plugin: 'io.spring.dependency-management'
	apply plugin: 'org.jetbrains.kotlin.jvm'
	apply plugin: 'org.jetbrains.kotlin.plugin.spring'
	apply plugin: 'com.diffplug.spotless'

	java {
		toolchain {
			languageVersion = JavaLanguageVersion.of(21)
		}
	}

	repositories {
		mavenCentral()
	}

	dependencyManagement {
		imports {
			mavenBom("org.springframework.boot:spring-boot-dependencies:3.3.1")
		}
	}

	kotlin {
		compilerOptions {
			freeCompilerArgs.addAll '-Xjsr305=strict'
		}
	}

	tasks.named('test') {
		useJUnitPlatform()
	}
}

spotless {
	format 'misc', {
		target '**/*.gradle', '.gitattributes', '.gitignore'

		trimTrailingWhitespace()
		indentWithTabs()
		endWithNewline()
	}
	kotlin {
		target '**/*.kt'
		ktfmt("0.49")
	}
}
